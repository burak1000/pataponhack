#include <Arduino.h>
#include <U8g2lib.h>
#include <Wire.h>
//#include "JoystickLib.h"
#include <Button.h>


#include "main.h"
#include "tools.h"




void setup() 
{
  pinMode(CROSS,INPUT);
  pinMode(SQUARE,INPUT);    
  pinMode(CIRCLE,INPUT);    
  pinMode(TRIANGLE,INPUT);  
  pinMode(RL_AN_PIN,INPUT);     
  pinMode(UD_AN_PIN,INPUT);     
  pinMode(R3_PIN,INPUT);  
  pinMode(BTN_BUILDIN,INPUT_PULLUP);
  UP_BUTTON.set_delay(80);
  UP_BUTTON.registeranalogfunc(stickisup);

  LEFT_BUTTON.set_delay(80);
  LEFT_BUTTON.registeranalogfunc(stickisleft);

  DOWN_BUTTON.set_delay(80);
  DOWN_BUTTON.registeranalogfunc(stickisdown);

  RIGHT_BUTTON.set_delay(80);
  RIGHT_BUTTON.registeranalogfunc(stickisright);

  R3_BUTTON.set_delay(50);
  R3_BUTTON.begin();
  M_BUTTON.set_delay(50);
  M_BUTTON.begin();
  u8g2.begin();
  //stick.calibrate();
  Serial.begin(9600);
  while (!Serial);
  u8g2.setFont(u8g2_font_9x15_t_symbols  );	// choose a suitable font
  u8g2.enableUTF8Print();
  u8g2.clearBuffer();
  u8g2.setCursor(0,32);
  u8g2.print(F("READY"));
  u8g2.sendBuffer();
  pinMode(A2,INPUT_PULLUP);
  setupTimer1ms();

}



void loop() 
{
  
  if(work_mode==mode_standbye)
  {
    mloop(0);
  }
  else if(work_mode==mode_play)
  {
    current_march=next_march;
    for (int i=0; i<4;i++)
    { 
        calcnextpulse();
        while (remainnextpulse()>0) mloop(i);
        PSBTN(marchs[current_march][i],PRESS);
        while (remainnextpulse()>-150) mloop(i);
        PSBTN(marchs[current_march][i],RELASE);
    }

    synctime+=4; 
    delay(4);

    calcnextpulse(); while (remainnextpulse()>0) mloop(3);
    calcnextpulse(); while (remainnextpulse()>0) mloop(3);
    calcnextpulse(); while (remainnextpulse()>0) mloop(3);
    calcnextpulse(); while (remainnextpulse()>0) mloop(3);
    
    
    
    
  }
  

}






void mloop(int index)
{
  
  if(work_mode==mode_play)
  {
    if(remainnextpulse()>=0) 
    {
      if (remainnextpulse()<60) return;
    }
    else 
    {
      if (remainnextpulse()<-60) return;
    }
  }
  
  if(R3_BUTTON.pressed()) 
  {
    work_mode=!work_mode;
    if(work_mode==mode_play) 
    {
      next_march=march_attack;
      synchronize();
      return;
    }
  }

//---------------------INPUT SECTION-----------------------
  if( work_mode==mode_play)
  {
    if(UP_BUTTON.pressed())  next_march=march_attack; 
    if(DOWN_BUTTON.pressed())  next_march=march_defense; 
    if(RIGHT_BUTTON.pressed())  next_march=march_foward; 
    if(LEFT_BUTTON.pressed())  next_march=march_backward; 
  }


//------------------------LCD SECTION-----------------------------
 
    u8g2.clearBuffer();					// clear the internal memory
    if( work_mode==mode_standbye)
    {
      u8g2.setCursor(0,16);
      u8g2.print(F("STANDBY"));
    }
    else if( work_mode==mode_play)
    {
      u8g2.setCursor(0,16);
      u8g2.print(F("NOW:"));
      printmarchname(current_march);

      u8g2.setCursor(0,36);
      u8g2.print(F("NEX:"));
      printmarchname(next_march);

      u8g2.setCursor(0,55);
      for(int i=0;i<=index;i++) printbtnname(marchs[current_march][i]);
    }
    u8g2.sendBuffer();					// transfer internal memory to the display
  
}

void printbtnname(uint8_t mpin)
{
  if(mpin==CROSS) u8g2.print(F("X "));
  if(mpin==CIRCLE) u8g2.print(F("\u25ef "));
  if(mpin==SQUARE) u8g2.print(F("\u2610 "));
  if(mpin==TRIANGLE) u8g2.print(F("\u25b3 "));
}

void printmarchname(uint8_t march_name)
{
  if (march_name==march_foward) u8g2.print(F("Foward"));
  else if (march_name==march_backward) u8g2.print(F("Backward"));
  else if (march_name==march_defense) u8g2.print(F("Defense"));
  else if (march_name==march_attack) u8g2.print(F("Attack"));
}


void PSBTN_logic(uint8_t pin,uint8_t mstate)//time basılı kalacağı süre
{
    if (mstate==PRESS)
    {
      pinMode(pin,OUTPUT);
      digitalWrite(pin,LOW);
      
    }
    else
    {
      digitalWrite(pin,HIGH);
    }
}

void PSBTN(uint8_t pin,uint8_t mstate)//time basılı kalacağı süre
{
    if (mstate==PRESS)
    {
      digitalWrite(pin,LOW);
      pinMode(pin,OUTPUT);
    }
    else
    {
      pinMode(pin,INPUT);  
    }
}

uint8_t stickisup()  { if(analogRead(UD_AN_PIN)<250) return 0; else return 1; }
uint8_t stickisdown() { if(analogRead(UD_AN_PIN)>700) return 0; else return 1; }
uint8_t stickisleft() { if(analogRead(RL_AN_PIN)<250) return 0; else return 1; }
uint8_t stickisright() { if(analogRead(RL_AN_PIN)>700) return 0; else return 1; }



ISR(TIMER1_COMPA_vect) 
{
  now++;
  //digitalWrite(PON,!digitalRead(PON));
}